# Arduino
## ESP8266
#### 컴퓨터에서 실습시 '모바일' 검색하여 노트북을 AP모드로 설정해논 뒤 esp를 접속시켜야함.
- Huzzah adafruit 모델 기준
- 툴 - 보드 - 보드매니저 - esp 검색, esp8266 패키지 설치되어 있어야함
- `Adafruit HUZZAH ESP8266` 보드로 선택(필수)
- arduinoWebSockets 라이브러리 필요

 https://github.com/Links2004/arduinoWebSockets
 - 핀 연결 순서 012345 (검0빨초흰0)

 ![esp_set](/assets/esp_set.jpg)

 - 아두이노에서 설정

 ![esp_tool](/assets/esp_tool.PNG)

 실행 코드 예시)


 (현재 실행 프로젝트는 service_udp임)

- UDP로 패킷을 안드로이드와 송수신 하는 코드
(filename : udp_example)

```
#include <ESP8266WiFi.h>
#include <WiFiUDP.h>

// wifi connection variables
const char* ssid = "ssid";   //공유기 id or 핸드폰 핫스팟설정 id
const char* password = "yourpassword";//공유기에서 설정한 password.
boolean wifiConnected = false;  //wifi connection 확인 변수. 초기값 false

// UDP variables
unsigned int localPort = 8888;//포트 선언.(안드로이드와 쌍으로 선언)
WiFiUDP UDP;                  //UDP 선언
boolean udpConnected = false; //udp connection 확인 변수. 초기값 false

char packetBuffer[UDP_TX_PACKET_MAX_SIZE];  //UDP 수신 buffer. char only
char ReplyBuffer[] = "acknowledged";        // *Recv로 보낼 데이터*
boolean connectWifi();
boolean connectUDP();

//setup에서 wifi, udp connection을 확인합니다.
void setup() {
  Serial.begin(115200);
  // Initialise wifi connection
  wifiConnected = connectWifi();

  // only proceed if wifi connection successful
  if(wifiConnected==true){
    udpConnected = connectUDP();
    if (udpConnected){
      // initialise pins
      pinMode(2,OUTPUT);    //2번 내장blue LED를 출력으로 선언
    }
  }
}

// connect to UDP – returns true if successful or false if not
boolean connectUDP(){
  boolean state = false;

  Serial.println("");
  Serial.println("Connecting to UDP");

  if(UDP.begin(localPort) == 1){
    Serial.println("Connection successful");
    state = true;
  }
  else{
    Serial.println("Connection failed");
  }
  return state;
}

boolean connectWifi(){
  boolean state = true;
  int i = 0;
  WiFi.begin(ssid, password); //begin은 공유기에 접속하는 함수
                              //자신이 서버가 된다면 softAP를 통해야함
  Serial.println("");
  Serial.println("Connecting to WiFi");

  // Wait for connection
  Serial.print("Connecting");
  while (WiFi.status() != WL_CONNECTED) { //연결이 되면 while문 탈출
    delay(2000);
    Serial.print(".");
    if (i > 10){
      state = false;  //10번의 try 시도
      break;
    }
    i++;
  }

  if (state){//연결이 완료되면 연결된 공유기 이름, IP 출력
    Serial.println("");
    Serial.print("Connected to ");
    Serial.println(ssid);
    Serial.print(" IP address: ");
    Serial.println(WiFi.localIP());
  }
  else {
    Serial.println("");
    Serial.println("Connection failed.");
  }
  return state;
}

//loop에서 UDP통신을 수행합니다.
void loop() {  
  if(wifiConnected){
    if(udpConnected){

      int packetSize = UDP.parsePacket(); //UDP 패킷 수신
      if(packetSize)//수신이 완료되면..
      {
        Serial.println("");
        Serial.print("Received packet of size ");
        Serial.println(packetSize);
        Serial.print("From ");
        IPAddress remote = UDP.remoteIP();
        for (int i =0; i < 4; i++)      //ip출력을 위한 반복문
        {
          Serial.print(remote[i], DEC);
          if (i < 3){
            Serial.print(".");
          }
        }

        Serial.print(", port ");
        Serial.println(UDP.remotePort());

        // ******read the packet into packetBufffer
        UDP.read(packetBuffer,UDP_TX_PACKET_MAX_SIZE);
        Serial.println("Contents: ");
        Serial.println(packetBuffer);                
        char value = packetBuffer[0];    //**안드로이드에서 설정 방식이 1byte통신이므로 이렇게함.

        // *******send a reply, to the IP address and port that sent us the packet we received
        UDP.beginPacket(UDP.remoteIP(), UDP.remotePort());
        UDP.write(ReplyBuffer);
        UDP.endPacket();
        Serial.print("value : ");
        Serial.println(value);
        // turn LED on or off depending on value recieved
        if(value == '1')  //안드로이드에서 1 송신시 LED On, 2송신시 LED Off
          digitalWrite(2,0);//LED On
         else
          digitalWrite(2,1);//LED Off
      }
      delay(100);  
    }
  }
}
```
![KakaoTalk_Video_20170402_2332_52_069](/assets/KakaoTalk_Video_20170402_2332_52_069.gif)

- 프로젝트 진행 사항 (7/9)
Service+UDP 안드로이드 소스 : esp의 데이터 수신하는 예
ServiceToast 안드로이드 소스: 버튼 리스너 코드로 실행 예제
service_upd 아두이노 소스 : esp 커넥션 예제

- 앱 실행시에 GPS가 켜져있으면 안됨.. 해당 부분 처리가 안되어 있슴.
- MainActivity에서 value 인자 값이 변함에 따라 emotion 행동을 취할 수 있도록 수정해야함.

- 진행사항 (8/6)
- 아두이노 출력창인 시리얼 모니터의 출력 배율이 소스코드와 일치해야 글자 깨지지 않음
